def pair = (
  // generics
  \T1: Prop. \T2: Prop.
  // structure
  all a: Prop.
    // constructors
    all intro: Prf T1 -> Prf T2 -> Prf a.
    a
);
def pair_intro = (
  // generics
  \T1: Prop. \T2: Prop.
  // arguments
  \v1: Prf T1. \v2: Prf T2.
  // value of (Prf either Left Right)
  \a: Prop.  \intro: Prf T1 -> Prf T2 -> Prf a.
    intro v1 v2
);

def either = (
  // generics
  \Left: Prop. \Right: Prop.
  // structure
  all a: Prop.
    // constructors
    all left: Prf Left -> Prf a.
    all right: Prf Right -> Prf a.
    a
);
def left = (
  // generics
  \Left: Prop. \Right: Prop.
  // arguments
  \v: Prf Left.
  // value of (Prf either Left Right)
  \a: Prop. \left: Prf Left -> Prf a. \right: Prf Right -> Prf a.
    left v
);
def right = (
  // generics
  \Left: Prop. \Right: Prop.
  // arguments
  \v: Prf Right.
  // value of (Prf either Left Right)
  \a: Prop. \left: Prf Left -> Prf a. \right: Prf Right -> Prf a.
    right v
);

// ⊤
def true = (
  // structure
  all a: Prop.
    // constructors
    all intro: Prf a.
    a
);
def true_intro = \a: Prop. \intro: Prf a. intro;
// ⊥
// Wow! it's interesting that in CC, natural false definition is the function that takes any type and return the value of that.
def false = (
  // structure (no constructor)
  all a: Prop.
    a
);

// >> Logic operators
// P ∧ Q
def and = \P: Prop. \Q: Prop. pair P Q;
// P ∨ Q
def or = \P: Prop. \Q: Prop. either P Q;
// P → Q
def implies = \P: Prop. \Q: Prop. all _: Prf P. Q;
// ¬P
def not = \P: Prop. implies P false;
// P ↔ Q
def iff = \P: Prop. \Q: Prop. and (implies P Q) (implies Q P);

// ========= end of preparation ========

// >> Provable theories (Intuitionistic Logic)
// P → ¬P → Q
def not2 = all P: Prop. all Q: Prop. implies P (implies (not P) Q);
def not2_proof = (\proof: Prf not2. proof) (
  // [answer]
  \P: Prop. \Q: Prop.
  \H1: Prf P. \H2: Prf (not P).
    H2 H1 Q
);
// ⊥ → P
def false2 = all P: Prop. implies false P;
def false2_proof = (\proof: Prf false2. proof) (
  // [answer]
  \P: Prop.
  \H1: Prf false.
    H1 P
);
// P → ¬¬P
def double_negation_intro = all P: Prop. implies P (not (not P));
def double_negation_intro_proof = (\proof: Prf double_negation_intro. proof) (
  // [answer]
  \P: Prop.
  \H1: Prf P.
    \H2: Prf P -> Prf false.
      H2 H1
);

// >> Classic Logic
// P ∨ ¬P
axiom classic_excluded_middle: pi P: Prop. Prf or P (not P);

// ¬¬P → P
def double_negation_elimination = all P: Prop. implies (not (not P)) P;
def double_negation_elimination_proof = (\proof: Prf double_negation_elimination. proof) (
  // [answer]
  \P: Prop.
    \H1: Prf not (not P).
      classic_excluded_middle P
      // [match P ∨ ¬P returning P]
      P
      (
        // [if P]
        \H2: Prf P.
        // [then obviously P]
        H2
      ) (
        // [if not P]
        \H2: Prf not P.
        // [then it contradicts, and it concludes anything including P]
        H1 H2 P
      )
);
// ((P → Q) → P) → P
def classic_peirce = all P: Prop. all Q: Prop. implies (implies (implies P Q) P) P;
def classic_peirce_proof = (\proof: Prf classic_peirce. proof) (
  // [answer]
  \P: Prop. \Q: Prop.
    \H1: Prf implies (implies P Q) P.
      classic_excluded_middle P
      // [match P ∨ ¬P returning P]
      P
      (
        // [if P]
        \H2: Prf P.
        // [then obviously P]
        H2
      ) (
        // [if not P]
        \H2: Prf not P.
        H1 (
          // [can supply P → Q]
          \H3: Prf P.
            // [if P, it contradicts with (not P), thus we can get Q]
            H2 H3 Q
        )
      )
);

// End of file
(\x: Prop. x)
